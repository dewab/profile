# Web Server
function web-server() {
    local flag_help
    local arg_shareport=(80)        # must be array for the [-1] reference below
    local arg_sharedir=(${PWD})     # must be array for the [-1] reference below
    local usage=(
        "web-server [-h,--help]"
        "web-server [-p <listenport>]  [-d <shared directory>]"
    )

    # -D pulls parsed flags out of $@
    # -E allows flags/args and positionals to be mixed, which we don't want in this example
    # -F says fail if we find a flag that wasn't defined
    # -M allows us to map option aliases (ie: h=flag_help -help=h)
    # -K allows us to set default values without zparseopts overwriting them
    # Remember that the first dash is automatically handled, so long options are -opt, not --opt
    zparseopts -D -E -M -F -K -- \
        {h,-help}=flag_help \
        p:=arg_shareport \
        d:=arg_sharedir ||
        return

    [[ -z "$flag_help" ]] || { print -l $usage && return }

    # DOCKER_CONTEXT=desktop-linux \
    docker run -it --rm \
    -p "$arg_shareport[-1]:80" \
    -v "$arg_sharedir[-1]":/usr/share/nginx/html \
    nginx
}
